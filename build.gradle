import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.defaults", version: "latest.release"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.maven.plugin.builder", version: "2.0.1"
		classpath group: "de.undercouch", name: "gradle-download-task", version: "5.4.0"
		classpath group: "org.ysb33r.gradle", name: "gradletest", version: "3.0.0-alpha.2"
		classpath group: "xalan", name: "xalan", version: "2.7.2"
	}

	repositories {
		mavenLocal()

		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}

		mavenCentral()
	}
}

apply plugin: "com.liferay.poshi.defaults"
apply plugin: "com.liferay.root.defaults.plugin"
apply plugin: 'java'

sourceCompatibility = "1.8"

dependencies {
	implementation group: "com.google.guava", name: "guava", version: "30.1-jre"
	implementation group: "com.googlecode.java-diff-utils", name: "diffutils", version: "1.2"
	implementation group: "commons-io", name: "commons-io", version: "2.6"
	implementation group: "dom4j", name: "dom4j", version: "1.6.1"
	implementation group: "junit", name: "junit", version: "4.12"
	implementation group: "mysql", name: "mysql-connector-java", version: "5.1.13"
	implementation group: "org.apache.ant", name: "ant", version: "1.9.4"
	implementation group: "org.apache.commons", name: "commons-lang3", version: "3.8.1"
	implementation group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.1"
	implementation group: "org.eclipse.jgit", name: "org.eclipse.jgit", version: "5.1.3.201810200350-r"
	implementation project(":jira-cloud-client")
	implementation project(":poshi-core")
	implementation project(":poshi-runner")
	implementation project(":sdk:gradle-plugins-poshi-runner")
	implementation project(":test:data-guard-connector")
}

repositories {
	maven {
		url "https://packages.atlassian.com/maven/repository/public"
	}

	mavenLocal()

	mavenCentral()
	 maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}

}

afterEvaluate {
	test.dependsOn 'downloadWebDriverBrowserBinary'
}

test {
    maxHeapSize = "1024m"
    minHeapSize = "1024m"

	systemProperty "webdriver.chrome.driver", project.buildDir.toString() + "/webdriver/chromedriver"
}

// copied from modules/build.gradle
//buildDir = new File(rootDir.parentFile, "build")

for (String gradleVersion in ["6.9.2", "7.3.3", "8.5"]) {
	String gradleDistributionURL = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
	String suffix = gradleVersion.replace(".", "")

	tasks.create(name: "extractGradleApi${suffix}", type: Copy) {
		description = "Extracts the Gradle ${gradleVersion} JAR files into a temporary directory."

		eachFile new StripPathSegmentsAction(1)

		from {
			zipTree(FileUtil.get(project, gradleDistributionURL))
		}

		include "gradle-${gradleVersion}/lib/**/*.jar"

		includeEmptyDirs = false
		into new File(buildDir, "gradle-${gradleVersion}")
	}

	tasks.create(name: "extractGradleSources${suffix}", type: Copy) {
		description = "Extracts the Gradle ${gradleVersion} sources into a temporary directory."

		eachFile new StripPathSegmentsAction(2)

		from {
			zipTree(FileUtil.get(project, gradleDistributionURL))
		}

		include "gradle-${gradleVersion}/src/**"

		includeEmptyDirs = false
		into new File(buildDir, "gradle-${gradleVersion}-sources")
	}
}

wrapper {
	distributionUrl = "https://releases-cdn.liferay.com/tools/gradle/gradle-7.3.3.LIFERAY-PATCHED-1-bin.zip"
}